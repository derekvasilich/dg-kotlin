plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.example.dgkotlin'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.dgkotlin"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        def propertiesFile = project.rootProject.file('local.properties')
        def mapsApiKey = System.getenv('APP_MAPS_API_KEY')
        def apiBaseUrl = System.getenv('APP_API_BASE_URL')
        def apiLegacyBaseUrl = System.getenv('APP_API_LEGACY_BASE_URL')
        def webViewQuoteUrl = System.getenv('APP_WEB_VIEW_QUOTE_URL')
        def webViewUrl = System.getenv('APP_WEB_VIEW_URL')
        if (propertiesFile.exists()) {
            properties.load(propertiesFile.newDataInputStream())
            mapsApiKey = properties.getProperty('app.maps-api-key')
            apiBaseUrl = properties.getProperty('app.api.base-url')
            apiLegacyBaseUrl = properties.getProperty('app.api.legacy-base-url')
            webViewQuoteUrl = properties.getProperty('app.web-view-quote.url')
            webViewUrl = properties.getProperty('app.web-view.url')
        }
        buildConfigField "String", "API_BASE_URL", "\"${apiBaseUrl}\""
        buildConfigField "String", "LEGACY_API_BASE_URL", "\"${apiLegacyBaseUrl}\""
        buildConfigField "String", "WEB_VIEW_QUOTE_URL", "\"${webViewQuoteUrl}\""
        buildConfigField "String", "WEB_VIEW_URL", "\"${webViewUrl}\""

        buildConfigField "String", "JWT_TOKEN_PREF_KEY", "\"jwt:token\""
        buildConfigField "String", "JWT_LEGACY_TOKEN_PREF_KEY", "\"jwt:legacy\""
        buildConfigField "String", "JWT_TYPE_PREF_KEY", "\"jwt:type\""
        buildConfigField "String", "REMEMBER_PREF_KEY", "\"remember:me\""
        buildConfigField "String", "USER_ID_PREF_KEY", "\"user:id\""
        buildConfigField "String", "USERNAME_PREF_KEY", "\"user:username\""

        manifestPlaceholders = [MAPS_API_KEY: "${mapsApiKey}"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    def camerax_version = "1.3.0-alpha06"
    def retrofit2_version = "2.3.0"

    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'

    implementation 'com.google.android.material:material:1.9.0'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.annotation:annotation:1.6.0'

    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    implementation 'com.google.mlkit:barcode-scanning:17.1.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "com.squareup.retrofit2:retrofit:${retrofit2_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit2_version}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit2_version}"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation 'com.google.code.gson:gson:2.8.9'
}